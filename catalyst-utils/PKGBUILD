# Partly based on original PKGBUILD by Vi0L0 <vi0l093@gmail.com>
# Great Contributor: Eduardo "kensai" Romero (previous maintainer)
# Contributor: csslayer
# Contributor: cvigano

# maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# maintainer: Roland Singer <roland[at]manjaro[dog]org>

pkgbase=catalyst-utils
pkgname=('catalyst-utils' 'opencl-catalyst' 'mhwd-catalyst')
pkgver=12.11
pkgrel=1
pkgdesc="AMD/ATI Catalyst drivers utilities and libraries with OpenCL implementation"
arch=('i686' 'x86_64')
url="http://www.ati.amd.com"
license=('custom')

source=(http://www2.ati.com/drivers/beta/amd-driver-installer-catalyst-${pkgver}-beta-x86.x86_64.zip
        atieventsd
        atieventsd.service
        signature.from.12.9)
sha256sums=('0c3e0c96b3daf8a6647ba2285374c96b7d57bc0ee69fbc7dc0c091cd482caffc'
            '9bc354e36e059cc2d120e35ec0dd893ddee347fdc7f02a9c99cd6569802b152e'
            'c410dac3cc3c0a468fcb7edfe1d271690eba46179b7dbf0eda8a12544871332f'
            'd6aff2bbed9d1ce24302cda420ca9d4f6d89e27b14ab4e9cc510245bc4aab743')

if [ "${CARCH}" = "i686" ]; then
  _ARCHDIR="x86"
  _ARCHLIB=""
  _ARCHXPIC=""
else
  _ARCHDIR="x86_64"
  _ARCHLIB="64"
  _ARCHXPIC="_64a"
fi

build() {
  cd ${srcdir}

  sh ./amd-driver-installer*.run --extract fglrx-install
}

package_catalyst-utils() {
  depends=('xorg-server>=1.13.0' 'xorg-server<1.14.0' 'netkit-bsd-finger' 'libxrandr' 'libsm' 'fontconfig' 'libxcursor' 'libxi' 'gcc-libs' 'libxinerama' 'mhwd' 'libgl')
  optdepends=('qt: amdcccle'
              'acpid: acpi event support')
  conflicts=('catalyst-legacy' 'catalyst-legacy-utils')
  provides=("libatical=${pkgver}")
  install=${pkgname}.install
  install -dm755 "${pkgdir}"/usr/{bin,sbin}
  install -dm755 "${pkgdir}"/usr/lib/{dri,xorg/modules/{dri,drivers,extensions/fglrx,linux},systemd/system}
  install -dm755 "${pkgdir}"/usr/include/GL
  install -dm755 "${pkgdir}"/etc/{ati,acpi/events,security/console.apps,rc.d}
  install -dm755 "${pkgdir}"/usr/share/{applications,ati/amdcccle,man/man8,pixmaps}
  
  # binaries
  cd ${srcdir}/fglrx-install
  install -m755 common/usr/sbin/* "${pkgdir}/usr/sbin/"
  install -m755 common/usr/X11R6/bin/* "${pkgdir}/usr/bin/"
  install -m755 arch/${_ARCHDIR}/usr/sbin/* "${pkgdir}/usr/sbin/"
  install -m755 arch/${_ARCHDIR}/usr/X11R6/bin/* "${pkgdir}/usr/bin/"
  
  # headers
  install -m644 common/usr/include/GL/* "${pkgdir}/usr/include/GL/"
  
  # xorg/dri/gl/... drivers
  cd xpic${_ARCHXPIC}/usr/X11R6/lib${_ARCHLIB}/modules
  install -m755 *.so "${pkgdir}/usr/lib/xorg/modules/"
  install -m755 drivers/*.so "${pkgdir}/usr/lib/xorg/modules/drivers/"
  install -m755 linux/*.so "${pkgdir}/usr/lib/xorg/modules/linux/"
  install -m755 extensions/fglrx/fglrx-libglx.so "${pkgdir}/usr/lib/xorg/modules/extensions/fglrx/"
  ln -s fglrx/fglrx-libglx.so "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so.catalyst"
  cd ${srcdir}/fglrx-install/arch/${_ARCHDIR}/usr/X11R6/lib${_ARCHLIB}
  install -m755 modules/dri/fglrx_dri.so "${pkgdir}/usr/lib/xorg/modules/dri/"
  ln -s /usr/lib/xorg/modules/dri/fglrx_dri.so "${pkgdir}/usr/lib/dri/"
  install -m755 fglrx/fglrx-libGL.so.1.2 "${pkgdir}/usr/lib/"
  install -m755 *.so* "${pkgdir}/usr/lib/"
  install -m644 libAMDXvBA.cap "${pkgdir}/usr/lib/"
  cd ${srcdir}/fglrx-install/arch/${_ARCHDIR}/usr/lib${_ARCHLIB}
  install -m755 *.so* "${pkgdir}/usr/lib/"
  # needed symlinks
  ln -s libatiuki.so.1.0 "${pkgdir}/usr/lib/libatiuki.so.1"
  ln -s fglrx-libGL.so.1.2 "${pkgdir}/usr/lib/libGL.so.catalyst"
  ln -s libAMDXvBA.so.1.0 "${pkgdir}/usr/lib/libAMDXvBA.so.1"
  ln -s libXvBAW.so.1.0 "${pkgdir}/usr/lib/libXvBAW.so"
  ln -s libfglrx_dm.so.1.0 "${pkgdir}/usr/lib/libfglrx_dm.so.1"
  
  # various files 
  cd ${srcdir}/fglrx-install/common
  install -m644 usr/share/man/man8/* "${pkgdir}/usr/share/man/man8/"
  install -m644 usr/share/applications/* "${pkgdir}/usr/share/applications/"
  install -m644 usr/share/icons/* "${pkgdir}/usr/share/pixmaps/"
  install -m644 usr/share/ati/amdcccle/* "${pkgdir}/usr/share/ati/amdcccle/"
  install -m644 etc/ati/* "${pkgdir}/etc/ati/"
  install -m755 "${srcdir}/fglrx-install/packages/Ubuntu/dists/source/replacements/authatieventsd.sh" "${pkgdir}/etc/ati/"
  install -m644 etc/security/console.apps/amdcccle-su "${pkgdir}/etc/security/console.apps/"
  install -m644 usr/share/applications/*.desktop "${pkgdir}/usr/share/applications/"
  # use ACPI example files
  install -m755 usr/share/doc/fglrx/examples/etc/acpi/*.sh "${pkgdir}/etc/acpi/"
  install -m644 usr/share/doc/fglrx/examples/etc/acpi/events/* "${pkgdir}/etc/acpi/events/"
  # systemd and rc.d scripts
  install -m755 "${srcdir}/atieventsd" "${pkgdir}/etc/rc.d/"
  install -m644 "${srcdir}/atieventsd.service" "${pkgdir}/usr/lib/systemd/system/"

  # provided in opencl-catalyst package
  rm "${pkgdir}"/usr/lib/lib{amdocl*,OpenCL}.so* 

  # we remove the watermark
  install -m644 "${srcdir}/signature.from.12.9" "${pkgdir}/etc/ati/signature"

  # licenses
  install -Dm644 "${srcdir}/fglrx-install/common/usr/share/doc/fglrx/LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.TXT"
  install -Dm644 "${srcdir}/fglrx-install/common/usr/share/doc/amdcccle/ccc_copyrights.txt" \
    "${pkgdir}/usr/share/licenses/${pkgname}/amdcccle_copyrights.txt"
}

package_opencl-catalyst() {
  pkgdesc="OpenCL implemention from AMD"
  provides=('libcl')
  conflicts=('libcl')
  depends=('gcc-libs')
  optdepends=('opencl-headers: headers necessary for OpenCL development')

  install -dm755 "${pkgdir}"/usr/{bin,lib}
  install -dm755 "${pkgdir}/etc/OpenCL/vendors"

  cd ${srcdir}/fglrx-install/arch/${_ARCHDIR}
  
  install -m755 usr/bin/clinfo "${pkgdir}/usr/bin/"
  install -m644 etc/OpenCL/vendors/amdocl*.icd "${pkgdir}/etc/OpenCL/vendors/"
  install -m755 usr/lib${_ARCHLIB}/libamdocl*.so "${pkgdir}/usr/lib/"
  install -m755 usr/lib${_ARCHLIB}/libOpenCL.so.1 "${pkgdir}/usr/lib/"
  ln -s libOpenCL.so.1 "${pkgdir}/usr/lib/libOpenCL.so"
  
  # license
  install -Dm644 "${srcdir}/fglrx-install/common/usr/share/doc/fglrx/LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.TXT"
}

package_mhwd-catalyst() {
    pkgdesc="MHWD module-ids for catalyst $pkgver"
    arch=('any')
    install -d -m755 "${pkgdir}/var/lib/mhwd/ids/pci/"
    # Generate mhwd database
    sh -e ${srcdir}/../mhwd-catalyst \
    ${srcdir}/fglrx-install/common/lib/modules/fglrx/build_mod/fglrxko_pci_ids.h \
    > ${pkgdir}/var/lib/mhwd/ids/pci/catalyst.ids
}
