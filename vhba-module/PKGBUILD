# Based on the file created for Arch Linux by:
# Ray Rashif <schiv@archlinux.org>
# Mateusz Herych <heniekk@gmail.com>
# Charles Lindsay <charles@chaoslizard.org>

# Maintainer: Guinux <nuxgui@gmail.com>
# Co-Maintainer: Clittle <philm@manjarolinux.org>

pkgname=vhba-module
pkgver=20110915
_extramodules=extramodules-3.2-MANJARO
pkgrel=5
pkgdesc="Kernel module that emulates SCSI devices"
arch=('i686' 'x86_64')
url="http://cdemu.sourceforge.net/"
license=('GPL')
depends=('linux>=3.2' 'linux<3.3')
makedepends=('linux-headers>=3.2')
install=$pkgname.install
source=("http://downloads.sourceforge.net/cdemu/$pkgname-$pkgver.tar.bz2"
        '60-vhba.rules')
md5sums=('8bb99b427ca67bad448f4dd211bdd1a2'
         '549bd2d9696bd1884c8eed7193c00e21')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  _kernver="$(cat /lib/modules/$_extramodules/version)"

  make -j1 KDIR=/usr/src/linux-$_kernver
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -D vhba.ko "$pkgdir/lib/modules/$_extramodules/vhba.ko"

  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extramodules'/" \
    "$startdir/vhba-module.install"

	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;

  install -Dm644 "$srcdir/60-vhba.rules" \
	  "$pkgdir/lib/udev/rules.d/60-vhba.rules"
}

# vim:set ts=2 sw=2 et:
