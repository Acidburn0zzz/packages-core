# Based on the file created for Arch Linux by:
# Sergej Pupykin <pupykin.s+arch@gmail.com>
# Krzysztof Raczkowski <raczkow@gmail.com>

# Maintainer: Guinux <nuxgui@gmail.com>
# Co-Maintainer: Clittle <philm@manjarolinux.org>

pkgname=linux35-open-vm-tools-modules
_pkgname=open-vm-tools-modules
epoch=1
pkgver=9.2.2
_pkgsubver=893683
pkgrel=1
pkgdesc="kernel modules for the open source implementation of VMware Tools"
arch=('i686' 'x86_64')
url="http://open-vm-tools.sourceforge.net/"
license=('GPL')
makedepends=('libdnet' 'icu' 'uriparser' 'linux35-headers')
depends=("linux35")
provides=("$_pkgname=$pkgver")
groups=('linux35-extramodules')
install=$_pkgname.install
options=('!strip')
source=("http://downloads.sourceforge.net/open-vm-tools/open-vm-tools-$pkgver-${_pkgsubver}.tar.gz"
	"modprobe.conf")
sha256sums=('1ae795e75bf4b38185f39083b8075686d3bab4c1222f4e39c863aeccb2f5f387'
         '27b83062b8b6a279e8bfb13f10566467377f38ac077e75933211912cac4a085d')

_extramodules=extramodules-3.5-MANJARO
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

build() {
  cd "$srcdir/open-vm-tools-${pkgver}-${_pkgsubver}"
  sed -i 's|proc-3.2.8|procps|g' configure
  [ $NOEXTRACT -eq 1 ] || ./configure \
      --prefix=/usr \
      --without-x \
      --with-linuxdir=/usr/lib/modules/$_kernver

  make -C modules modules
}

package() {
  depends=("linux35")

  cd "$srcdir/open-vm-tools-${pkgver}-${_pkgsubver}"
  mkdir -p $pkgdir/usr/lib/modules/${_extramodules}/
  for MOD in `find -type f -name '*.ko'`; do
    install -Dm644 $MOD $pkgdir/usr/lib/modules/${_extramodules}/
  done
  gzip $pkgdir/usr/lib/modules/${_extramodules}/*.ko

  install -D -m 644 ${srcdir}/modprobe.conf ${pkgdir}/etc/modprobe.d/${pkgname}.conf
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extramodules'/" "${startdir}/${_pkgname}.install"
}
