# Based on the file created for Arch Linux by:
# Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Bob Fanger < bfanger(at)gmail >
# Filip <fila pruda com>, Det < nimetonmaili(at)gmail >

# Maintainer: Guinux <nuxgui@gmail.com>
# Co-Maintainer: Clittle <philm@manjarolinux.org>

pkgname=linux32-r8168
_pkgname=r8168
groups=('linux32-extramodules')
pkgver=8.028.00
pkgrel=2
pkgdesc="A kernel module for Realtek 8168 network cards"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' 'linux32')
makedepends=('linux32-headers')
source=(http://r8168.googlecode.com/files/$_pkgname-$pkgver.tar.bz2)
install=$_pkgname.install

_extramodules=extramodules-3.2-MANJARO
_kernver="$(cat /usr/lib/modules/$_extramodules/version || true)"

build() {
	cd "$srcdir/$_pkgname-$pkgver"
	make clean SYSSRC=/usr/src/linux-$_kernver modules
}

package() {
	cd "$srcdir/$_pkgname-$pkgver/src"
	install -D -m644 $_pkgname.ko "$pkgdir/usr/lib/modules/$_extramodules/$_pkgname.ko"

	# set the kernel we've built for inside the install script
	sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/g" "${startdir}/${_pkgname}.install"

	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}

md5sums=('4cc3c1706f00b55ab9bd6f5e06b758c2')
sha256sums=('c77873f43693eea5304e17e35ff3d6503f6581e358d28c38927573ea56f4d6a3')
