# Based on the file created for Arch Linux by:
# Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# SpepS <dreamspepser at yahoo dot it>
# Tobias Powalowski <tpowa@archlinux.org>

# Maintainer: Guinux <nuxgui@gmail.com>
# Co-Maintainer: Clittle <philm@manjarolinux.org>

_extramodules=extramodules-3.2-MANJARO
_kver="$(cat /usr/lib/modules/${_extramodules}/version)"
_pkgname=ndiswrapper
pkgname=linux32-ndiswrapper
groups=('linux32-extramodules')
pkgver=1.57
pkgrel=2
pkgdesc="Module for NDIS (Windows Network Drivers) drivers supplied by vendors."
license=('GPL')
arch=('i686' 'x86_64')
url="http://ndiswrapper.sourceforge.net"
install=ndiswrapper.install
depends=('linux32' 'wireless_tools' 'perl')
makedepends=('linux32-headers')
provides=("$_pkgname-utils" "$_pkgname-bin")
replaces=("_$_pkgname-utils" "$_pkgname-bin")
source=("http://downloads.sourceforge.net/sourceforge/$_pkgname/$_pkgname-$pkgver.tar.gz")
options=('!strip')
md5sums=('7a401dc540938bf07893c67f418b6152')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  # modinfo path fix
  sed -i "/modinfo/s/s/usr\//" driver/Makefile

  make KVERS=$_kver
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make INST_DIR="lib/modules/$_extramodules" \
    KVERS=$_kver DESTDIR="$pkgdir/" install

  # set the kernel we've built for inside the install script
  sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/g" "${startdir}/${_pkgname}.install"

  gzip "$pkgdir/usr/lib/modules/$_extramodules/$_pkgname.ko"
}

# vim:set ts=2 sw=2 et:
