# Based on the file created for Arch Linux by:
# Maintainer : Thomas Baechler <thomas@archlinux.org>

# Maintainer: Guinux <nuxgui@gmail.com>
# Co-Maintainer: Clittle <philm@manjarolinux.org>

pkgname=('linux32-nvidia')
_pkgname=nvidia
groups=('linux32-extramodules')
pkgver=302.17
_extramodules=extramodules-3.2-MANJARO
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
pkgrel=5
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
makedepends=('linux32-headers')
license=('custom')
options=(!strip)

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	sha256sums=('3525d3814361bd202998cea2962bb3d1bb3419206d02fd8ac30d61358c1a284f')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    sha256sums=('728c6c0c27f4a90186b47622416c28b492da84d28930cf3c4e01942644c04f71')
fi

source=(${source[@]} "nvidia-blacklist-vga-pmu-registers-256-304.diff")
sha256sums=(${sha256sums[@]} '674a51e77beb3e076dd29b1d9a37ee23e1498ab128490ea2efb2140e45b44fde')

build() {
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}/kernel"
    patch < "${srcdir}/nvidia-blacklist-vga-pmu-registers-256-304.diff"
    make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
    pkgdesc="NVIDIA drivers for linux32."
    depends=('linux32' "nvidia-utils=${pkgver}")
    conflicts=('nvidia-96xx' 'nvidia-173xx')
    groups=('nvidia-kernel-modules')
    install=nvidia.install

    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
