# Maintainer: Guillaume <guillaume@manjaro.org>
# Maintainer: Roland <roland@manjaro.org>
# Maintainer: Philip MÃ¼ller <philm@manjaro.org>

pkgname=pacman-mirrorlist
pkgver=20130710
pkgrel=2
pkgdesc="Manjaro Linux mirror list for use by pacman"
arch=('any')
depends=('python')
url="https://git.manjaro.org/core/pacman-mirrors"
license=('GPL')
install=pacman-mirrorlist.install
backup=(etc/pacman-mirrors.conf)
_git=yes


getsource() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $1 ] ; then
    if [ "$_git" == "yes" ] ; then
       cd $1 && git pull origin master
    else
       cd $1 && git pull origin master && git checkout -b $pkgver
    fi
    msg "The local files are updated."
  else
    git clone $2 $1
    if [ "$_git" != "yes" ] ; then
       cd $1 && git checkout -b $pkgver $pkgver
    fi
  fi

  msg "GIT checkout done or server timeout"
}

prepare() {
  getsource "pacman-mirrors" "git://git.manjaro.org/core/pacman-mirrors.git"
}

package() {
  cd "${srcdir}"/pacman-mirrors
  make DESTDIR="${pkgdir}" install
  # Those mirrors seems to be broken
  rm "${pkgdir}"/etc/pacman.d/mirrors/China
}
