#
# maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# maintainer: Roland Singer <roland[at]manjaro[dog]org>

_extramodules=extramodules-3.7-MANJARO
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

pkgname=linux37-catalyst
_pkgname=catalyst
_beta=beta11
pkgver=12.11
pkgrel=3
pkgdesc="AMD/ATI Catalyst drivers for linux. fglrx kernel module only"
arch=('i686' 'x86_64')
url="http://www.ati.amd.com"
license=('custom')
depends=('linux37' 'catalyst-utils')
makedepends=('gcc-libs' 'gcc>4.0.0' 'make' 'linux37-headers')
conflicts=('linux37-catalyst-legacy')
provides=("$_pkgname=$pkgver")
groups=('linux37-extramodules')
install=catalyst.install

source=(http://www2.ati.com/drivers/beta/amd-driver-installer-catalyst-${pkgver}-${_beta}-x86.x86_64.zip
        ati_make.sh
        makefile_compat.patch
        arch-fglrx-3.7.patch)

sha256sums=('d76edd98ed2ca4dfda63cff9060a3e367f32b4cc9a13590f91aba853d85b847b'
	 'e1d3cedbfc6a239d5d71458c458137f1a7404ebddfa838557d25c965dd49c845'
	 'd344df75ffbe9bd899e288862f41983c2e24b5eff3e7e8cf02d4891eeb137fff'
         '4a93aab0f829be4474a869fa43dd80e2d43348929e2236872a70040c3bbee4f5'
)

build() {

    msg "Setting compilation variables ..."
  ## include ati_make.sh and use _ati_check function from it
    . ati_make.sh
    _ati_check


    msg "Unpacking archive ..."
    sh ./amd-driver-installer*.run --extract fglrx-install


    if [ "${CARCH}" = "x86_64" ]; then
	BUILDARCH=x86_64
	_archdir=x86_64
    fi
    if [ "${CARCH}" = "i686" ]; then
	BUILDARCH=i386
	_archdir=x86
    fi

    cd "${srcdir}/fglrx-install/"


    msg "Applying patch(es) ..."
   ## makefile patch to choose arch_compat_alloc_user_space or older compat_alloc_user_space
   ## works only in combination with ati_make.sh script
    patch -Np1 -i ../makefile_compat.patch

    patch -Np1 -i ../arch-fglrx-3.7.patch || return 1

    cd "${srcdir}/fglrx-install/common/lib/modules/fglrx/build_mod"
    cp "${srcdir}/fglrx-install/arch/${_archdir}/lib/modules/fglrx/build_mod/libfglrx_ip.a" . || return 1
    cp 2.6.x/Makefile . || return 1


    msg "Compiling fglrx module ..."
   ## Compile module using _ati_check variables
    make -C /usr/lib/modules/${_kernver}/build SUBDIRS="`pwd`" ARCH=${BUILDARCH} \
      MODFLAGS="-DMODULE -DATI -DFGL -DPAGE_ATTR_FIX=$PAGE_ATTR_FIX -DCOMPAT_ALLOC_USER_SPACE=$COMPAT_ALLOC_USER_SPACE $def_smp $def_modversions" \
      PAGE_ATTR_FIX=$PAGE_ATTR_FIX COMPAT_ALLOC_USER_SPACE=$COMPAT_ALLOC_USER_SPACE modules || return 1
}

package() {
    cd "${srcdir}/fglrx-install/common/lib/modules/fglrx/build_mod"
    install -m755 -d "${pkgdir}/usr/lib/modules/${_extramodules}/"
    install -m644 fglrx.ko "${pkgdir}/usr/lib/modules/${_extramodules}/"
    find "${pkgdir}" -name '*.ko' -exec gzip -9 {} \;
    sed -i -e "s/KERNEL_VERSION=.*/KERNEL_VERSION=${_kernver}/" $startdir/*.install
    sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/" $startdir/*.install

    ## License
    install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
    install -m644 "${srcdir}/fglrx-install/LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}/"
}
