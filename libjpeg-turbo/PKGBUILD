#
# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Maintainer: Roland Singer <roland[at]manjaro[dog]org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Simone Sclavi 'Ito' <darkhado@gmail.com>

pkgname=libjpeg-turbo
pkgver=1.2.1
pkgrel=2
pkgdesc="libjpeg derivative with accelerated baseline JPEG compression and decompression"
arch=('i686' 'x86_64')
url="http://libjpeg-turbo.virtualgl.org/"
license=('GPL' 'custom')
depends=('glibc')
makedepends=('nasm')
provides=('libjpeg=8.0.2')
conflicts=('libjpeg')
replaces=('libjpeg' 'turbojpeg')
options=('!libtool')
source=(http://sourceforge.net/projects/$pkgname/files/$pkgver/$pkgname-$pkgver.tar.gz)
sha1sums=('a4992e102c6d88146709e8e6ce5896d5d0b5a361')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # put documentation in a reasonable directory...
  sed -i "/docdir = /s#/doc#/doc/libjpeg-turbo#" Makefile.in
  sed -i "/exampledir = /s#/doc#/doc/libjpeg-turbo#" Makefile.in

  ./configure --prefix=/usr --with-jpeg8 --mandir=/usr/share/man
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  make test
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # provide jpegint.h as it is required by various software
  install -m644 jpegint.h "$pkgdir/usr/include/"


  install -dm755 "$pkgdir/usr/share/licenses/libjpeg-turbo/"
  ln -s ../../doc/libjpeg-turbo/README \
    "$pkgdir/usr/share/licenses/libjpeg-turbo/README"
  ln -s ../../doc/libjpeg-turbo/README-turbo.txt \
    "$pkgdir/usr/share/licenses/libjpeg-turbo/README-turbo.txt"
}

