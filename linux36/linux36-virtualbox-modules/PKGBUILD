# Based on the file created for Arch Linux by:
# Ionut Biru <ibiru@archlinux.org>

# Maintainer: Guinux <nuxgui@gmail.com>
# Co-Maintainer: Clittle <philm@manjarolinux.org>

pkgbase=linux36-virtualbox-modules
pkgname=('linux36-virtualbox-modules' 'linux36-virtualbox-manjaro-modules')
pkgver=4.1.18
pkgrel=0.2
arch=('i686' 'x86_64')
url='http://virtualbox.org'
license=('GPL')
makedepends=('libstdc++5' 'bin86' 'dev86' 'iasl' 'libxslt' 'libxml2' 'libpng' 'libidl2' 'xalan-c' 'sdl' 'linux36-headers')
[[ $CARCH == "x86_64" ]] && makedepends=("${makedepends[@]}" 'gcc-multilib' 'lib32-glibc')
source=(http://download.virtualbox.org/virtualbox/${pkgver}/VirtualBox-${pkgver}.tar.bz2
         LocalConfig.kmk 60-vboxguest.rules)
sha256sums=('e650e4fdc23581b9edc0e5d5705cc596c76796851ebf65ccda0edb8e413fa3b7'
            '81fb0837385e034b2eee03c94a80708557e5e070cc5b84fd9dbd9ace80c075ba'
            '033c597e0f5285d2ddb0490868e5b6f945f45c7b1b1152a02a9e6fea438b2c95')

_extramodules=extramodules-3.6-MANJARO
_kernver="$(cat /usr/lib/modules/${_extramodules}/version || true)"

export KERN_DIR=/usr/lib/modules/${_kernver}/build
export KERN_INCL=/usr/src/linux-${_kernver}/include/

build() {
    cd "$srcdir/VirtualBox-${pkgver}"

    cp "$srcdir/LocalConfig.kmk" .

    ./configure \
        --with-linux=/usr/src/linux-${_kernver} \
        --disable-java \
        --disable-docs \
        --disable-xpcom \
        --disable-python \
        --disable-sdl-ttf \
        --disable-alsa \
        --disable-pulse \
        --disable-dbus  \
        --disable-opengl \
        --build-headless \
        --nofatal
    source ./env.sh
    kmk all

    make -C "$srcdir/VirtualBox-${pkgver}/out/linux.$BUILD_PLATFORM_ARCH/release/bin/src"
    make -C "$srcdir/VirtualBox-${pkgver}/out/linux.$BUILD_PLATFORM_ARCH/release/bin/additions/src"
}

package_linux36-virtualbox-manjaro-modules(){
    pkgdesc="Additions only for Manjaro Linux guests (kernel modules)"
    license=('GPL')
    install=virtualbox-manjaro-modules.install
    depends=('linux36')
    provides=("virtualbox-archlinux-modules=$pkgver")
    groups=('linux36-extramodules')

    source "$srcdir/VirtualBox-${pkgver}/env.sh"

    cd "$srcdir/VirtualBox-${pkgver}/out/linux.$BUILD_PLATFORM_ARCH/release/bin/additions/src"

    for module in vboxguest.ko vboxsf.ko vboxvideo.ko; do
        install -D -m644 ${module} \
            "$pkgdir/usr/lib/modules/${_extramodules}/${module}"
    done

    install -D -m 0644 "$srcdir/60-vboxguest.rules" \
        "$pkgdir/usr/lib/udev/rules.d/60-linux36-vboxguest.rules"

    find "${pkgdir}" -name '*.ko' -exec gzip -9 {} \;

    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "$startdir/virtualbox-manjaro-modules.install"
}

package_linux36-virtualbox-modules(){
    pkgdesc="Kernel modules for VirtualBox"
    license=('GPL')
    install=virtualbox-modules.install
    depends=('linux36')
	groups=('linux36-extramodules')

    source "$srcdir/VirtualBox-${pkgver}/env.sh"


    cd "$srcdir/VirtualBox-${pkgver}/out/linux.$BUILD_PLATFORM_ARCH/release/bin/src"

    install -D -m644 vboxdrv.ko \
        "$pkgdir/usr/lib/modules/${_extramodules}/vboxdrv.ko"

    install -D -m644 vboxnetadp.ko \
        "$pkgdir/usr/lib/modules/${_extramodules}/vboxnetadp.ko"

    install -D -m644 vboxnetflt.ko \
        "$pkgdir/usr/lib/modules/${_extramodules}/vboxnetflt.ko"

    install -D -m644 vboxpci.ko \
        "$pkgdir/usr/lib/modules/${_extramodules}/vboxpci.ko"

    find "${pkgdir}" -name '*.ko' -exec gzip -9 {} \;

    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "$startdir/virtualbox-modules.install"
}
