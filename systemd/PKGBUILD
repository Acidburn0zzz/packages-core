# Based on the file created for Arch Linux by:
# Dave Reisner <dreisner@archlinux.org>

# Maintainer:  Phil MÃ¼ller <philm@manajro.org>
# Contributor: Guinux <nuxgui@gmail.com>

pkgname=systemd
pkgver=198
pkgrel=3
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2' 'LGPL2.1' 'MIT')
groups=('base')
makedepends=('acl' 'cryptsetup' 'dbus-core' 'docbook-xsl' 'gobject-introspection' 'gperf'
             'intltool' 'kmod' 'libcap' 'libgcrypt' 'libmicrohttpd' 'libxslt' #'gtk-doc'
             'linux-api-headers' 'pam' 'python' 'quota-tools' 'xz')
options=('!libtool')
pkgdesc="system and service manager"
depends=('acl' 'bash' 'dbus-core' 'glib2' 'kbd' 'kmod' 'hwids' 'libcap' 'libgcrypt'
         'pam' 'util-linux' 'xz' 'sysvinit-tools')
provides=("libsystemd=$pkgver" 'nss-myhostname' "systemd-tools=$pkgver" "udev=$pkgver" "systemd-sysvcompat=$pkgver" 'systemd-manjaro-units')
replaces=('libsystemd' 'nss-myhostname' 'systemd-tools' 'udev' 'systemd-sysvcompat' 'systemd-manjaro-units')
conflicts=('libsystemd' 'nss-myhostname' 'systemd-tools' 'udev' 'systemd-sysvcompat' 'systemd-manjaro-units')
optdepends=('cryptsetup: required for encrypted block devices'
            'libmicrohttpd: systemd-journal-gatewayd'
            'quota-tools: kernel-level quota management'
            'python: systemd library bindings')
backup=(etc/dbus-1/system.d/org.freedesktop.systemd1.conf
        etc/dbus-1/system.d/org.freedesktop.hostname1.conf
        etc/dbus-1/system.d/org.freedesktop.login1.conf
        etc/dbus-1/system.d/org.freedesktop.locale1.conf
        etc/dbus-1/system.d/org.freedesktop.timedate1.conf
        etc/systemd/system.conf
        etc/systemd/user.conf
        etc/systemd/logind.conf
        etc/systemd/journald.conf
        etc/udev/udev.conf
        etc/rc.local
        etc/rc.local.shutdown)
install="systemd.install"
source=("http://www.freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.xz"
        'initcpio-hook-udev'
        'initcpio-install-udev'
        'initcpio-install-timestamp'
        'use-split-usr-path.patch'
        'rc-local-shutdown.service'
        '0001-journal-pass-the-pid-to-sd_pid_get_owner_uid.patch'
        '0001-strv-fix-STRV_FOREACH_PAIR-macro-definition.patch'
        '0001-rules-move-builtin-calls-before-the-permissions-sect.patch'
        '0001-Fix-vacuum-logic-error.patch')
sha256sums=('444492355e5ff0ad99e0691ecaff1081ee8d45901580f47ba8b74e56107c71bf'
            '892ce43218e0a458981bbce451252c8987dc398e60b8de288e7542b8f2409c13'
            'e39a9e3d50024466f5b98486a53858b166953efeee451e386eecac9501696c00'
            'b6a7cd6512209ea6b53fec2ad4aff23573ecd36d698d7da8baa5c8278fa1a119'
            '7b440cd7e08257ad12766eb39ab3bf25ab232735a93858c9f66886d984780081'
            '9ffae90f53644a6c1f81c82d28cce2533a5c5365ec34976af7aa13bf59aff7b3'
            '4d4e82a1ec89143c5a19f832e449817f03f6f14e0934e593807e130e9af1a9e8'
            '9169bc5afbdbf7e12543fe5e0eb6f11e4d8009e1e549e750a4ebae7a14460f7d'
            '30dc207f927a957d41cb889f8ce973df2a8503b329b60224a12245f5bb23f4ba'
            '73631bdb8973596f7d095639e2f773ebf02b5578331baaecb996a161d41ca215'
)

build() {
  cd "$pkgname-$pkgver"

  # hang onto this until we do the /{,s}bin merge
  patch -Np1 <"$srcdir/use-split-usr-path.patch"

  # upstream commit 83d7d83bcc6c462ecbb4c8003e3a8b41f3b88d46
  patch -Np1 <"$srcdir"/0001-journal-pass-the-pid-to-sd_pid_get_owner_uid.patch

  # upstream commit 961e4526925b7b1e1d3582f2fc9fb38035e2b5fb
  patch -Np1 <"$srcdir"/0001-strv-fix-STRV_FOREACH_PAIR-macro-definition.patch

  # upstream commit bbb7f2ae5035105575365750592caa87213d7072
  # - modified to avoid pulling in disabling of firmware loading
  patch -Np1 <"$srcdir"/0001-rules-move-builtin-calls-before-the-permissions-sect.patch

  # upstream commit 6c142648aaced56ab681fcc97a71b06d588122a9
  patch -Np1 <"$srcdir"/0001-Fix-vacuum-logic-error.patch

  ./configure \
      --enable-static \
      --libexecdir=/usr/lib \
      --localstatedir=/var \
      --sysconfdir=/etc \
      --enable-introspection \
      --disable-gtk-doc \
      --disable-audit \
      --disable-ima \
      --disable-tcpwrap

  make
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install

  printf "d /run/console 0755 root root\n" > "$pkgdir/usr/lib/tmpfiles.d/console.conf"

  install -dm755 "$pkgdir/bin"
  ln -s ../usr/lib/systemd/systemd "$pkgdir/bin/systemd"

  # don't write units to /etc by default -- we'll enable this on post_install
  # as a sane default
  rm "$pkgdir/etc/systemd/system/getty.target.wants/getty@tty1.service"
  rmdir "$pkgdir/etc/systemd/system/getty.target.wants"

  # get rid of RPM macros
  rm -r "$pkgdir/etc/rpm"

  # the path to udevadm is hardcoded in some places
  install -d "$pkgdir/sbin"
  ln -s ../usr/bin/udevadm "$pkgdir/sbin/udevadm"

  # udevd is no longer udevd because systemd. why isn't udevadm now udevctl?
  ln -s ../lib/systemd/systemd-udevd "$pkgdir/usr/bin/udevd"

  # add back tmpfiles.d/legacy.conf
  install -m644 "systemd-$pkgver/tmpfiles.d/legacy.conf" "$pkgdir/usr/lib/tmpfiles.d"

  # Replace dialout/tape/cdrom group in rules with uucp/storage/optical group
  sed -i 's#GROUP="dialout"#GROUP="uucp"#g;
          s#GROUP="tape"#GROUP="storage"#g;
          s#GROUP="cdrom"#GROUP="optical"#g' "$pkgdir"/usr/lib/udev/rules.d/*.rules

  # add mkinitcpio hooks
  install -Dm644 "$srcdir/initcpio-install-udev" "$pkgdir/usr/lib/initcpio/install/udev"
  install -Dm644 "$srcdir/initcpio-hook-udev" "$pkgdir/usr/lib/initcpio/hooks/udev"
  install -Dm644 "$srcdir/initcpio-install-timestamp" "$pkgdir/usr/lib/initcpio/install/timestamp"

  # make sysvcompat symlinks
  install -dm755 "$pkgdir/sbin"
  for tool in runlevel reboot shutdown poweroff halt telinit; do
    ln -s '/usr/bin/systemctl' "$pkgdir/sbin/$tool"
  done
  ln -s '../usr/lib/systemd/systemd' "$pkgdir/sbin/init"

  # install rc-local-shutdown.service
  install -Dm644 "${srcdir}/rc-local-shutdown.service" "${pkgdir}/usr/lib/systemd/system/rc-local-shutdown.service"
}

# vim: ft=sh syn=sh et
