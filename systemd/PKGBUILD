# Based on the file created for Arch Linux by:
# Dave Reisner <dreisner@archlinux.org>

# Maintainer: Guinux <nuxgui@gmail.com>

pkgbase=systemd
pkgname=('systemd' 'libsystemd')
pkgver=44
pkgrel=5
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2')
makedepends=('acl' 'cryptsetup' 'dbus-core' 'docbook-xsl' 'gperf' 'intltool'
             'kmod' 'libcap' 'libxslt' 'linux-api-headers' 'pam' 'udev' 'xz')
options=('!libtool')
source=("http://www.freedesktop.org/software/systemd/systemd-$pkgver.tar.xz"
        "os-release"
        "0001-check-for-proper-return-from-dirent_ensure_type.patch"
        "0001-logind-close-FIFO-before-ending-sessions-cleanly.patch"
        "0001-util-never-follow-symlinks-in-rm_rf_children.patch")
sha256sums=('7a5aac4b4b8b3a82bf59292f10e43d8f2c2d7039f34e95714f81d8edcb42233c'
            '0cee1cf4781e9f5f07171801aa58f3d4a363b894476ab3cb8e8b22af97ef99c7'
            'c2a99eed24e262d26df77de3a0c8e7b56aa4887b0d0302fc8fe5ba43cce80656'
            '0ae64dd027044c9d833d174c7642dc5013ace1357e489f361533c0ffa3db2a0c'
            '4bf6f7d0312f07614d2addece6b48f3301bb85de5f27c8b9729e11b1f4d9e36a')

build() {
  cd "$pkgname-$pkgver"

  # https://bugzilla.redhat.com/show_bug.cgi?id=803358
  patch -Np1 <"$srcdir/0001-util-never-follow-symlinks-in-rm_rf_children.patch"

  # https://bugs.archlinux.org/task/28386
  patch -Np1 <"$srcdir/0001-logind-close-FIFO-before-ending-sessions-cleanly.patch"

  # Fix broken 'systemctl list-unit-files' (upstream fb5ef067c49)
  patch -Np1 <"$srcdir/0001-check-for-proper-return-from-dirent_ensure_type.patch"

  ./configure --sysconfdir=/etc \
              --libexecdir=/usr/lib \
              --libdir=/usr/lib \
              --with-pamlibdir=/lib/security \
              --localstatedir=/var \
              --with-rootprefix= \
              --enable-plymouth \
              --disable-gtk \
              --disable-selinux \
              --disable-tcpwrap \
              --disable-audit \
              --with-distro=arch

  make
}

package_systemd() {
  pkgdesc="systemd and service manager"
  groups=('base')
  depends=('acl' 'dbus' 'dbus-core' "libsystemd=$pkgver" 'kbd' 'kmod' 'libcap' 'pam' 
           'util-linux' 'udev' 'xz' 'dbus-python' 'python2-cairo' 'systemd-manjaro-units')
  optdepends=('cryptsetup: required for encrypted block devices')
  backup=(etc/dbus-1/system.d/org.freedesktop.systemd1.conf
          etc/dbus-1/system.d/org.freedesktop.hostname1.conf
          etc/dbus-1/system.d/org.freedesktop.login1.conf
          etc/dbus-1/system.d/org.freedesktop.locale1.conf
          etc/dbus-1/system.d/org.freedesktop.timedate1.conf
          etc/systemd/system.conf
          etc/systemd/user.conf
          etc/systemd/systemd-logind.conf
          etc/systemd/systemd-journald.conf)
  install="$pkgname.install"

  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/os-release" "$pkgdir/etc/os-release"

  printf "d /run/console 755 root root\n" >"$pkgdir/usr/lib/tmpfiles.d/console.conf"
  chmod 644 "$pkgdir/usr/lib/tmpfiles.d/console.conf"

  # symlink to /bin/systemd for compat and sanity
  ln -s ../lib/systemd/systemd "$pkgdir/bin/systemd"
  
  # add required symlinks
  install -dm755 "$pkgdir/sbin"
  for tool in runlevel reboot shutdown poweroff halt telinit; do
    ln -s '/bin/systemctl' "$pkgdir/sbin/$tool"
  done

  ln -s '../lib/systemd/systemd' "$pkgdir/sbin/init"

  # use python2 for systemd-analyze
  sed -i '1s/python$/python2/' "$pkgdir/usr/bin/systemd-analyze"

  # didn't build this...
  rm -f "$pkgdir/usr/share/man/man1/systemadm.1"

  # fix .so links in manpage stubs
  find "$pkgdir/usr/share/man" -type f -name '*.[[:digit:]]' \
      -exec sed -i '1s|^\.so \(.*\)\.\([[:digit:]]\+\)|.so man\2/\1.\2|' {} +

  ### split off libsystemd (libs, includes, pkgconfig, man3)
  install -dm755 "$srcdir"/libsystemd/usr/{include,lib/pkgconfig}

  cd "$srcdir"/libsystemd
  mv "$pkgdir/usr/lib"/libsystemd-*.so* usr/lib
  mv "$pkgdir/usr/include/systemd" usr/include
  mv "$pkgdir/usr/lib/pkgconfig"/libsystemd-*.pc usr/lib/pkgconfig
  
  
  # Fix local environment in gettys
  sed -i "s/\(Environment=LANG=\)/#\1/" "$pkgdir"/lib/systemd/system/getty@.service
}

package_libsystemd() {
  pkgdesc="systemd client libraries"
  depends=('libcap' 'xz')

  mv "$srcdir/libsystemd"/* "$pkgdir"
}

# vim: ft=sh syn=sh et
