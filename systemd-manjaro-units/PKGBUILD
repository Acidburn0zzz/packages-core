# Based on the file created for Arch Linux by:
# Dave Reisner <d@falconindy.com>

# Maintainer: Guinux <nuxgui@gmail.com>

pkgname=systemd-manjaro-units
pkgver=20120704
pkgrel=4
pkgdesc="Manjaro specific Systemd unit files"
arch=('any')
license=('MIT')
depends=('plymouth')
conflicts=('systemd-httpd-units')
provides=('systemd-httpd-units')
replaces=('systemd-httpd-units')
install=systemd-manjaro-units.install
source=("systemd-arch-units-$pkgver.tar.gz::https://www.github.com/falconindy/systemd-arch-units/tarball/$pkgver"
        'rc-local.service'
        'rc-local-shutdown.service'
        'httpd.conf'
        'httpd.service')
sha256sums=('6b45a0c607a864e051a0b9f9f413a079f1d2498498eaa11be4d6834f0dcace69'
            '2e27fd62ec5e579e65f779710db2ddb10adf744077caabc2c15b44d040d0fc97'
            '308cd05122a3d30529e74b8562f89ee8abda9b40e4c2f890262ccb25097087b0'
            '15cb5d58c9693ad432df4350e260154e492d247c85a7e1d7e6dd80c9a3c58d81'
            '70732bc6b45dd8a35738c3c1b570703593e27001df87d882964eb671a6362028')
            
package() {
  dirname=$(tar tf "$srcdir/systemd-arch-units-$pkgver.tar.gz" | sed 1q)
  make -C "$srcdir/$dirname" DESTDIR="$pkgdir" install
  
  ln -sf /usr/lib/systemd/system/gdm-plymouth.service "${pkgdir}/usr/lib/systemd/system/gdm.service"
  install -Dm644 "${srcdir}/rc-local.service" "${pkgdir}/usr/lib/systemd/system/rc-local.service"
  install -Dm644 "${srcdir}/rc-local-shutdown.service" "${pkgdir}/usr/lib/systemd/system/rc-local-shutdown.service"
  install -Dm644 "${srcdir}/httpd.service" "${pkgdir}/usr/lib/systemd/system/httpd.service"
  install -Dm644 "${srcdir}/httpd.conf" "${pkgdir}/etc/tmpfiles.d/httpd.conf"
}
