# Maintainer: Roland <roland@manjaro.org>
# Maintainer: Clittle <philm@manjaro.org>

pkgname=bumblebee-3.0
pkgver=3.0.1
pkgrel=1
pkgdesc="Bumblebee brings Optimus Support for Linux Through VirtualGL or Primus."
arch=('i686' 'x86_64')
conflicts=('bumblebee')
provides=("bumblebee=$pkgver")
depends=('primus' 'glib2' 'mesa-libgl')
optdepends=('bbswitch: switch on/off discrete card' 'virtualgl: alternative back-end for optirun')
if [ "$CARCH" = "x86_64" ]; then
	depends[${#depends[@]}]='lib32-primus'
	optdepends[${#optdepends[@]}]='lib32-virtualgl: alternative back-end for optirun to run 32bit applications'
fi
makedepends=('virtualgl' 'help2man')

url="http://www.Bumblebee-Project.org"
license=("GPL3")
backup=('etc/bumblebee/bumblebee.conf' 
    'etc/bumblebee/xorg.conf.nouveau' 
    'etc/bumblebee/xorg.conf.nvidia')
source=("https://github.com/downloads/Bumblebee-Project/Bumblebee/bumblebee-${pkgver}.tar.gz" "10-evdev.conf" "10-quirks.conf" "bumblebee_xorg_config_dir_fix_patch.diff" 'xorg-113.patch')



build() {
    cd "${srcdir}/bumblebee-${pkgver}"

	# Patch Xorg config dir patch
	patch -p0 < "${srcdir}/bumblebee_xorg_config_dir_fix_patch.diff"

	# Patch Xorg 1.13
	patch -p1 < "${srcdir}/xorg-113.patch"

    ./configure \
        CONF_DRIVER_MODULE_NVIDIA=nvidia \
        CONF_LDPATH_NVIDIA=/usr/lib/nvidia-bumblebee:/usr/lib32/nvidia-bumblebee \
        CONF_MODPATH_NVIDIA=/usr/lib/nvidia-bumblebee/xorg/,/usr/lib/xorg/modules \
        CONF_GID=users \
        --prefix=/usr \
        --sysconfdir=/etc \
        --without-pidfile

    make
}



package() {
    cd "${srcdir}/bumblebee-${pkgver}"
    make install DESTDIR="$pkgdir"

    # Install systemd unit
    install -D -m644 "scripts/systemd/bumblebeed.service" "${pkgdir}/usr/lib/systemd/system/bumblebeed.service"

    # Make bash_completion work
    mv -v "${pkgdir}/etc/bash_completion.d/bumblebee" "${pkgdir}/etc/bash_completion.d/optirun"

    # Install bumblebees xorg.conf.d files
	install -D -m755 "${srcdir}/10-evdev.conf" "${pkgdir}/etc/X11/bumblebee.xorg.conf.d/10-evdev.conf"
	install -D -m755 "${srcdir}/10-quirks.conf" "${pkgdir}/etc/X11/bumblebee.xorg.conf.d/10-quirks.conf"

	# Adjust ServerGroup to users
	sed -i "/#/!s|ServerGroup[ ]*=.*|ServerGroup=users|g" "${pkgdir}/etc/bumblebee/bumblebee.conf"
}


sha256sums=('447eebbcfbc8877a7a15126b492f07b9186428e75c768b1eeb86ce71076f3e60'
            '979ae1ed10ece98fdf9812ae3fe7198859703e6b96829373f7b7bf445a7cf6b1'
            '94612f5c0d34a3b7152915c2e285c7b462e9d8e38d3539bd551a339498eac166'
            'b770378d76dcb0c96512bdb941b3cc39786cbedc175e8888dcd163c47e27f706'
            '141b1a56bee025955b040e9f0bb2e4a3d0fab88c4bf7493ce037af06d7a037aa')
