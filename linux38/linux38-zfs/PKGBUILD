# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

_extramodules=extramodules-3.8-MANJARO
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

pkgname=('linux38-zfs')
pkgver=0.6.1
pkgrel=2
pkgdesc="Kernel modules for the Zettabyte File System."
depends=(linux38-spl=$pkgver zfs-utils=$pkgver 'linux38' 'mkinitcpio')
makedepends=('linux38-headers')
provides=("zfs-modules=$pkgver")
arch=('i686' 'x86_64')
url='http://zfsonlinux.org/'
source=(http://archive.zfsonlinux.org/downloads/zfsonlinux/zfs/zfs-$pkgver.tar.gz)
groups=('manjarozfs' 'linux38-extramodules')
md5sums=('822cd73c139d89369d6c3944f8afe659')
license=('CDDL')
install=zfs.install

build() {
  cd ${srcdir}/zfs-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --sbindir=/usr/sbin \
              --libdir=/usr/lib \
              --datadir=/usr/share \
              --includedir=/usr/include \
              --with-udevdir=/lib/udev \
              --libexecdir=/usr/lib/zfs-$pkgver \
              --with-config=kernel \
              --with-linux=/usr/src/linux-$_kernver \
              --with-linux-obj=/usr/src/linux-$_kernver
  make
}

package() {
  cd ${srcdir}/zfs-$pkgver
  make DESTDIR=${pkgdir} install
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/zfs.install"
  sed -i -e "s/linux='.*'/linux=38/" "${startdir}/zfs.install"
  sed -i -e "s|${srcdir}/||g" "${pkgdir}/usr/src/zfs-$pkgver/${_kernver}/Module.symvers"
  mkdir -p "${pkgdir}/usr/lib/modules/${_extramodules}"
  for i in avl  nvpair  unicode  zcommon  zfs  zpios; do
     cp ${pkgdir}/lib/modules/${_kernver}/extra/${i}/*.ko "${pkgdir}/usr/lib/modules/${_extramodules}"
  done
  gzip ${pkgdir}/usr/lib/modules/${_extramodules}/*.ko
  rm -R "${pkgdir}/lib"
}
