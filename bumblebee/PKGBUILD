# Maintainer: Roland <roland@manjaro.org>
# Maintainer: Clittle <philm@manjaro.org>

pkgname=bumblebee
pkgver=3.0.1
pkgrel=1
pkgdesc="Bumblebee brings Optimus Support for Linux through VirtualGL."
arch=('i686' 'x86_64')
depends=('virtualgl' 'glib2' 'systemd' 'libbsd')
optdepends=('xf86-video-nouveau: Nouveau driver' 'nouveau-dri: 3D acceleration features fo Nouveau' 'mesa: 3D acceleration features for Nouveau'  'bbswitch: switch on/off discrete card' 'nvidia: Nvidia kernel driver')

if [ "$CARCH" = "x86_64" ]; then
     optdepends[${#optdepends[@]}]='lib32-virtualgl: run 32bit applications with optirun'
fi

url="http://www.Bumblebee-Project.org"
license=("GPL3")
backup=('etc/bumblebee/bumblebee.conf' 
    'etc/bumblebee/xorg.conf.nouveau' 
    'etc/bumblebee/xorg.conf.nvidia')
source=("10-evdev.conf" "10-quirks.conf" "bumblebee_config_patch.diff" "bumblebee_xorg_config_dir_fix_patch.diff" "https://github.com/downloads/Bumblebee-Project/Bumblebee/${pkgname}-${pkgver}.tar.gz")


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

	# Patch Xorg config dir patch
	patch -p0 < "${srcdir}/bumblebee_xorg_config_dir_fix_patch.diff"

	# Patch configs
	patch -p1 < "${srcdir}/bumblebee_config_patch.diff"

    ./configure \
        CONF_DRIVER_MODULE_NVIDIA=nvidia \
        CONF_LDPATH_NVIDIA=/usr/lib/nvidia-bumblebee:/usr/lib32/nvidia-bumblebee \
        CONF_MODPATH_NVIDIA=/usr/lib/nvidia-bumblebee/xorg/,/usr/lib/xorg/modules \
        CONF_GID=users \
        --prefix=/usr \
        --sysconfdir=/etc
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make install DESTDIR="$pkgdir"
    install -D -m755 "scripts/systemd/bumblebeed.service" "$pkgdir/usr/lib/systemd/system/bumblebeed.service"

    # Make bash_completion work
    mv -v "${pkgdir}/etc/bash_completion.d/bumblebee" "${pkgdir}/etc/bash_completion.d/optirun"

	# Install bumblebees xorg.conf.d files
	install -D -m755 "${srcdir}/10-evdev.conf" "${pkgdir}/etc/X11/bumblebee.xorg.conf.d/10-evdev.conf"
	install -D -m755 "${srcdir}/10-quirks.conf" "${pkgdir}/etc/X11/bumblebee.xorg.conf.d/10-quirks.conf"
}


md5sums=('198c5598e32818f6524f56c55596378c'
         'd4f7dfc6be4ef4e2c6dd7632a9d88abe'
         '20c955c5e40f57e0133a274057dd51f0'
         'b607b630f620d20484d700a164840286'
         '7b3fd4bfa0d59e877abdb75cbdba30f8')
