# Maintainer: Roland <roland@manjaro.org>
# Maintainer: Clittle <philm@manjaro.org>
# Contributor: Samsagax <samsagax@gmail.com>

pkgname=bumblebee
pkgver=3.0.1
pkgrel=3
pkgdesc="Bumblebee brings Optimus Support for Linux through VirtualGL."
arch=('i686' 'x86_64')
depends=('virtualgl' 'glib2' 'systemd' 'libbsd')
optdepends=('xf86-video-nouveau: Nouveau driver' 'nouveau-dri: 3D acceleration features fo Nouveau' 'mesa: 3D acceleration features for Nouveau'  'bbswitch: switch on/off discrete card' 'nvidia: Nvidia kernel driver')

if [ "$CARCH" = "x86_64" ]; then
     optdepends[${#optdepends[@]}]='lib32-virtualgl: run 32bit applications with optirun'
fi

url="http://www.Bumblebee-Project.org"
license=("GPL3")
backup=('etc/bumblebee/bumblebee.conf' 
    'etc/bumblebee/xorg.conf.nouveau' 
    'etc/bumblebee/xorg.conf.nvidia')
source=("10-evdev.conf" "10-quirks.conf" "bumblebee_xorg_config_dir_fix_patch.diff" "https://github.com/downloads/Bumblebee-Project/Bumblebee/${pkgname}-${pkgver}.tar.gz" '99-remove-nvidia-dev.rules')


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

	# Patch Xorg config dir patch
	patch -p0 < "${srcdir}/bumblebee_xorg_config_dir_fix_patch.diff"

    ./configure \
        CONF_DRIVER_MODULE_NVIDIA=nvidia \
        CONF_LDPATH_NVIDIA=/usr/lib/nvidia-bumblebee:/usr/lib32/nvidia-bumblebee \
        CONF_MODPATH_NVIDIA=/usr/lib/nvidia-bumblebee/xorg/,/usr/lib/xorg/modules \
        CONF_GID=users \
        --prefix=/usr \
        --sysconfdir=/etc

    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make install DESTDIR="$pkgdir"

    # Install systemd unit
    install -D -m644 "scripts/systemd/bumblebeed.service" "${pkgdir}/usr/lib/systemd/system/bumblebeed.service"    

    # Make bash_completion work
    mv -v "${pkgdir}/etc/bash_completion.d/bumblebee" "${pkgdir}/etc/bash_completion.d/optirun"

    #Install udev rule to prevent GH-#144
    install -D -m644 "$srcdir/99-remove-nvidia-dev.rules" "${pkgdir}/lib/udev/rules.d/99-remove-nvidia-dev.rules"

	# Install bumblebees xorg.conf.d files
	install -D -m755 "${srcdir}/10-evdev.conf" "${pkgdir}/etc/X11/bumblebee.xorg.conf.d/10-evdev.conf"
	install -D -m755 "${srcdir}/10-quirks.conf" "${pkgdir}/etc/X11/bumblebee.xorg.conf.d/10-quirks.conf"

	# Adjust ServerGroup to users
	sed -i "/#/!s|ServerGroup[ ]*=.*|ServerGroup=users|g" "${pkgdir}/etc/bumblebee/bumblebee.conf"
}

sha256sums=('d4c8044c6b27cdd8e7366127f5ba2f006f7a2e4b7e3b49255d4ba513ab3d334c'
            '94612f5c0d34a3b7152915c2e285c7b462e9d8e38d3539bd551a339498eac166'
            'b770378d76dcb0c96512bdb941b3cc39786cbedc175e8888dcd163c47e27f706'
            '447eebbcfbc8877a7a15126b492f07b9186428e75c768b1eeb86ce71076f3e60'
            'bd8d8ae490171a9302f85c07e7fc95522dc71615779304f05a01588ae96508cb')
