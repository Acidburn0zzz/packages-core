# Based on the file created for Arch Linux by:
# Dan McGee <dan@archlinux.org>
# Dave Reisner <dave@archlinux.org>

# Maintainer: Guinux <nuxgui@gmail.com>

pkgname=pacman
pkgver=4.0.3
pkgrel=5
pkgdesc="A library-based package manager with dependency support"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base')
depends=('bash' 'glibc>=2.15' 'libarchive>=3.0.2' 'curl>=7.19.4'
         'gpgme' 'pacman-mirrorlist' 'archlinux-keyring' 'manjaro-keyring')
makedepends=('asciidoc')
optdepends=('fakeroot: for makepkg usage as normal user')
backup=(etc/pacman.conf etc/makepkg.conf)
install=pacman.install
options=(!libtool)
source=(ftp://ftp.archlinux.org/other/pacman/$pkgname-$pkgver.tar.gz{,.sig}
        0001-Add-conflict-for-replacing-owned-empty-directory.patch
        0002-Check-empty-subdirectory-ownership.patch
        pacman.conf
        pacman.conf.x86_64
        makepkg.conf)

sha256sums=('a48696ae51aaccf31a69ea7916ba9b06e7836eb933b8a281c666729c9d55ce25'
            '87d0f36ffa218b4bf22b7522d6464ffb47441f7360feb517c05cf82622d122c3'
            '4a64e2aa188314e640e7ed34898b4611aac386fe42a52720d220779a86003c17'
            'a547c8daaf12e79ff8d0fd7a29a5a020095b1ac8eb17b4cc8e572600e08afb7a'
            '707adad444b1110761e0f549fb4943147655ae0ea2b3e51b12a0756bcf973801'
            'e1e0dcec959198b97e1144c71d41402dab2ddc7e8d104507a3a75d1b6abf51cd'
            'bcc448268679843a05ca1486fb1f2eff14a230338b1f59c427cbf1fd806c0259')

# keep an upgrade path for older installations
#PKGEXT='.pkg.tar.gz'

build() {
  cd $srcdir/$pkgname-$pkgver

  patch -p1 -i $srcdir/0001-Add-conflict-for-replacing-owned-empty-directory.patch
  patch -p1 -i $srcdir/0002-Check-empty-subdirectory-ownership.patch

  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var --enable-doc
  make
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install

  # install Arch specific stuff
  mkdir -p $pkgdir/etc
  case "$CARCH" in
    i686)
      install -m644 $srcdir/pacman.conf $pkgdir/etc/pacman.conf
      mycarch="i686"
      mychost="i686-pc-linux-gnu"
      myflags="-march=i686"
      ;;
    x86_64)
      install -m644 $srcdir/pacman.conf.x86_64 $pkgdir/etc/pacman.conf
      mycarch="x86_64"
      mychost="x86_64-unknown-linux-gnu"
      myflags="-march=x86-64"
      ;;
  esac
  install -m644 $srcdir/makepkg.conf $pkgdir/etc/
  # set things correctly in the default conf file
  sed -i $pkgdir/etc/makepkg.conf \
    -e "s|@CARCH[@]|$mycarch|g" \
    -e "s|@CHOST[@]|$mychost|g" \
    -e "s|@CARCHFLAGS[@]|$myflags|g"

  # install completion files
  install -Dm644 contrib/bash_completion "$pkgdir/usr/share/bash-completion/completions/pacman"
  for f in makepkg pacman-key; do
    ln -s pacman "$pkgdir/usr/share/bash-completion/completions/$f"
  done

  install -Dm644 contrib/zsh_completion $pkgdir/usr/share/zsh/site-functions/_pacman
}
