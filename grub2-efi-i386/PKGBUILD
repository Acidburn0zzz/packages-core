# Based on the file created for Arch Linux by:
# Ronald van Haren <ronald.archlinux.org>
# Keshav P R <(the.ridikulus.rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>

# Maintainer: Guinux <nuxgui@gmail.com>

_grub_bzr_rev="4169"
_pkgver="2.00~beta4"

_grub_lua_ver=24
_grub_gpxe_ver=12

pkgname=grub2-efi-i386
pkgver=2.00beta4
pkgrel=1
epoch=1
pkgdesc="The GNU GRand Unified Bootloader version 2 - i386 UEFI version"
url="http://www.gnu.org/software/grub/"
arch=('any')
license=('GPL3')

makedepends=('xz' 'python' 'autogen' 'texinfo' 'help2man' 'gettext' 'device-mapper' 'fuse')
depends=("grub2-common=${epoch}:${pkgver}" 'dosfstools' 'efibootmgr')

options=('!strip' '!emptydirs')

# source=("ftp://ftp.archlinux.org/other/grub2/grub_r${_grub_bzr_rev}.tar.xz"
source=("http://alpha.gnu.org/gnu/grub/grub-${_pkgver}".tar.xz
        "ftp://ftp.archlinux.org/other/grub2/grub2_extras_lua_r${_grub_lua_ver}.tar.xz"
        "ftp://ftp.archlinux.org/other/grub2/grub2_extras_gpxe_r${_grub_gpxe_ver}.tar.xz"
        'ldflags.patch')

noextract=("grub2_extras_lua_r${_grub_lua_ver}.tar.xz"
           "grub2_extras_gpxe_r${_grub_gpxe_ver}.tar.xz")

sha1sums=('96bfdf4d53767faaf2c744ee82d4d703e4b672a2'
          '89290031b974780c6df76893836d2477d4add895'
          'b04994c005910b4bf123f034b30109d5c8d6fd86'
          'ef68a1b0f50174633fb738b8fb8c2e66844ece2d')
	
build() {
	cd "${srcdir}/grub-${_pkgver}"

	# fix LDFLAGS
	patch -Np1 -i ${srcdir}/ldflags.patch

	## add grub-extras
	export GRUB_CONTRIB="${srcdir}/grub-${_pkgver}/grub-extras/" 
	install -d "${srcdir}/grub-${_pkgver}/grub-extras"

	bsdtar xf "${srcdir}/grub2_extras_lua_r${_grub_lua_ver}.tar.xz" \
		-C "${srcdir}/grub-${_pkgver}/grub-extras"

	bsdtar xf "${srcdir}/grub2_extras_gpxe_r${_grub_gpxe_ver}.tar.xz" \
		-C "${srcdir}/grub-${_pkgver}/grub-extras"

	## Requires python2
	# sed 's|python |python2 |g' -i "${srcdir}/grub-${_pkgver}/autogen.sh"

	## start the actual build process
	cd "${srcdir}/grub-${_pkgver}/"
	./autogen.sh

	CFLAGS="" ./configure \
		--with-platform="efi" \
		--target="i386" \
		--host="${CARCH}-unknown-linux-gnu" \
		--disable-efiemu \
		--enable-mm-debug \
		--enable-nls \
		--enable-device-mapper \
		--enable-cache-stats \
		--enable-grub-mkfont \
		--enable-grub-mount \
		--prefix="/usr" \
		--bindir="/usr/bin" \
		--sbindir="/usr/sbin" \
		--mandir="/usr/share/man" \
		--infodir="/usr/share/info" \
		--datarootdir="/usr/share" \
		--sysconfdir="/etc" \
		--program-prefix="" \
		--with-bootdir="/boot" \
		--with-grubdir="grub" \
		--disable-werror


	CFLAGS="" make

}

package() {

	cd "${srcdir}/grub-${_pkgver}"
	make DESTDIR="${pkgdir}/" install 

	## remove non platform-specific files
	rm -rf "${pkgdir}"/{boot,etc,usr/{share,bin,sbin}}

	## remove gdb debugging related files
	rm -f "${pkgdir}/usr/lib/grub/i386-efi"/*.module || true
	rm -f "${pkgdir}/usr/lib/grub/i386-efi"/*.image || true
	rm -f "${pkgdir}/usr/lib/grub/i386-efi"/{kernel.exec,gdb_grub,gmodule.pl} || true

}
