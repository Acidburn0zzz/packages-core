# Based on the file created for Arch Linux by:
# Maintainer : Thomas Baechler <thomas@archlinux.org>

# Maintainer: Guinux <nuxgui@gmail.com>
# Co-Maintainer: Clittle <philm@manjarolinux.org>

pkgbase=linux32-nvidia
pkgname=('linux32-nvidia' 'mhwd-nvidia')
_pkgname=nvidia
pkgver=295.49
_extramodules=extramodules-3.2-MANJARO
_kernver="$(cat /lib/modules/${_extramodules}/version)"
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
makedepends=('linux32-headers')
license=('custom')
options=(!strip)

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums=('fd998f870d20b30b8d636ffce79d9e6c')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
   _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums=('84b92aa6eec13a38e8748264138acc56')
fi

build() {
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}/kernel"
    make SYSSRC=/lib/modules/"${_kernver}/build" module
}

package_linux32-nvidia() {
    pkgdesc="NVIDIA drivers for linux32."
    depends=('linux32' "nvidia-utils=${pkgver}")
    replaces=("$_pkgname=$pkgver")
    conflicts=('nvidia-96xx' 'nvidia-173xx' "$_pkgname=$pkgver")
    install=nvidia.install

    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/lib/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/lib/modprobe.d/nvidia.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
}

package_mhwd-nvidia() {
    pkgdesc="MHWD module-ids for nvidia $pkgver"
    arch=('any')
    install -d -m755 "${pkgdir}/var/lib/mhwd"
    # Generate mhwd database
    sh -e ${srcdir}/../mhwd-nvidia \
    ${srcdir}/${_pkg}/README.txt \
    ${srcdir}/${_pkg}/kernel/nv-kernel.o \
    > ${pkgdir}/var/lib/mhwd/nvidia.mhwd
}
