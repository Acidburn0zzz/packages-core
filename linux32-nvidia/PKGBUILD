# Based on the file created for Arch Linux by:
# Maintainer : Thomas Baechler <thomas@archlinux.org>

# Maintainer: Guinux <nuxgui@gmail.com>
# Co-Maintainer: Clittle <philm@manjarolinux.org>

pkgname=('linux32-nvidia')
_pkgname=nvidia
pkgver=295.53
_extramodules=extramodules-3.2-MANJARO
_kernver="$(cat /lib/modules/${_extramodules}/version)"
pkgrel=2
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
makedepends=('linux32-headers')
license=('custom')
options=(!strip)

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	md5sums=('af353274ab078cb90aba428ca1f00f0e')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums=('c974ee20fc016c9fa9ef39fc2e298f8a')
fi

build() {
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}/kernel"
    make SYSSRC=/lib/modules/"${_kernver}/build" module
}

package() {
    pkgdesc="NVIDIA drivers for linux32."
    depends=('linux32' "nvidia-utils=${pkgver}")
    provides=("$_pkgname=$pkgver")
    replaces=("$_pkgname=$pkgver")
    conflicts=('nvidia-96xx' 'nvidia-173xx')
    groups=('nvidia-kernel-modules')
    install=nvidia.install

    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
}
