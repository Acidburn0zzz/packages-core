# Based on the file created for Arch Linux by:
# Tom Gundersen <teg@jklm.no>

# Maintainer: Roland <roland@manjarolinux.org>

pkgname=filesystem
pkgver=2011.12
pkgrel=2
pkgdesc='Base filesystem'
arch=('any')
license=('GPL')
url='http://www.manjarolinux.org'
groups=('base')
install='filesystem.install'
depends=('iana-etc' 'bash' 'coreutils')
backup=('etc/fstab' 'etc/crypttab' 'etc/group' 'etc/hosts' 'etc/ld.so.conf' 'etc/passwd'
        'etc/shadow' 'etc/gshadow' 'etc/resolv.conf' 'etc/motd' 'etc/nsswitch.conf'
        'etc/shells' 'etc/host.conf' 'etc/securetty' 'etc/profile' 'etc/issue')
source=('group' 'issue' 'nsswitch.conf' 'securetty' 'host.conf' 'ld.so.conf'
        'passwd' 'shadow' 'fstab' 'crypttab' 'hosts' 'motd' 'resolv.conf' 'shells'
        'gshadow' 'profile' 'modprobe.d.usb-load-ehci-first' 'rc.local' 'rc.local.shutdown')
sha256sums=('c74c53d61d283650c343fd80d5e5a586b8fc6601ff258e3abfcd983b3c0c2f63'
            'aa59e888f2f4b6f565ae7f4057b987bfde07890a2ccde438abee2b93a93d96c0'
            '504d3ac929bcee8a609a3b4f1b67e9d7bb061433b51229c00b807665237b1bb7'
            'f7b38167196943d8fb9d2e75358c7262d5fe1082b257c4c8caffe3b6de429cb5'
            'f21f35e7d3083aeee19beb48478130d119b2572922a4889df98326d551540eda'
            '00d5d103837480d26cd350753382c3211df6bc304ee59ffaab130fad108f6a86'
            '61f3203e6b7d666e22d6d4d75f3c688640c2905186641aaa285d332db0e1456a'
            '34b820ca01b9ad7e4792e92122b39e891e647c016b7ecccb220d6f4e72eee5c7'
            '37700fd6a4a18a00c75cd86e3c55f7affaed937a88b95dee6c7dda3d46cd4f5e'
            '7ee98c477fec37ebf6f52fc6a1c4d4c017d1fe4f8e54b414828da33634711aae'
            '13d1f59c1d0860bf43da996e2b9a6ab2744791614241ed9a1376b7e7e30de3a8'
            'e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855'
            '74d2be3dd69600bcf0da751a1804a386bf4ad143b76d79c336acaa5c600e7991'
            '6cff886fe9e23096d5f67b7857a747ebaf10431be5a2afc40236de5ad13a654f'
            '01284869dc22e4d2e8ee953dbbf0e0fdf9455042d5133fda1d276399badba993'
            'd8d4d3eb8e2bc1f64de5b0d33cd6864558c0fe149cedb479fad36e0523060e60'
            '6486258d7667a9dc389658cf45ca535e14ea472db2f7fe425932cfa2e5a59da1'
            '156b75b3470c2f73efca084b7ee681eca01508df3d8e9ba0c9325162b5d80f11'
            '4367e4f2b40237448cad791410a1acaedcd46f5504827840aa85214fad1b546c')

package() {
	cd ${srcdir}

	#
	# setup root filesystem
	#
	for d in bin boot dev etc home lib/modules media mnt sbin usr var opt srv/http sys run; do
		install -d -m755 ${pkgdir}/${d}
	done
	install -d -m555 ${pkgdir}/proc
	install -d -m0750 ${pkgdir}/root
	install -d -m1777 ${pkgdir}/tmp
	# vsftpd won't run with write perms on /srv/ftp
	install -d -m555 -g ftp ${pkgdir}/srv/ftp

	# setup /etc
	install -d ${pkgdir}/etc/{ld.so.conf.d,skel,profile.d}
	for f in fstab group host.conf hosts issue ld.so.conf motd nsswitch.conf passwd resolv.conf securetty shells profile; do
		install -m644 ${srcdir}/${f} ${pkgdir}/etc/
	done
	ln -s /proc/self/mounts ${pkgdir}/etc/mtab
	for f in gshadow shadow crypttab; do
		install -m600 ${srcdir}/${f} ${pkgdir}/etc/
	done

	# add rc.local and rc.local.shutdown
	install -m755 ${srcdir}/rc.local ${pkgdir}/etc/
	install -m755 ${srcdir}/rc.local.shutdown ${pkgdir}/etc/

	# Create the manjaro-release file
	echo "Manjaro Linux" > ${pkgdir}/etc/manjaro-release

	install -D -m644 ${srcdir}/modprobe.d.usb-load-ehci-first ${pkgdir}/lib/modprobe.d/usb-load-ehci-first.conf

	# setup /var
	for d in cache/man local opt run log/old lib/misc empty; do
		install -d -m755 ${pkgdir}/var/${d}
	done
	install -d -m1777 ${pkgdir}/var/{lock,tmp,spool/mail}
	# allow setgid games to write scores
	install -d -m775 -g games ${pkgdir}/var/games
	ln -s spool/mail ${pkgdir}/var/mail
	# prevent pacman from removing directory (FS#16886)
	touch ${pkgdir}/var/empty/.keep

	#
	# setup /usr hierarchy
	#
	for d in bin include lib sbin share/misc src; do
		install -d -m755 ${pkgdir}/usr/${d}
	done
	for d in $(seq 8); do
		install -d -m755 ${pkgdir}/usr/share/man/man${d}
	done


	#
	# setup /usr/local hierarchy
	#
	for d in bin etc games include lib man sbin share src; do
		install -d -m755 ${pkgdir}/usr/local/${d}
	done
	ln -s ../man ${pkgdir}/usr/local/share/man
}
