# Based on the file created for Arch Linux by:
# Tom Gundersen <teg@jklm.no>

# Maintainer: Guinux <nuxgui@gmail.com>
# Maintainer: Roland <roland@manjaro.org>
# Maintainer: Philip MÃ¼ller <philm@manjaro.org>

pkgname=filesystem
pkgver=2012.12
pkgrel=1
pkgdesc='Base filesystem'
arch=('any')
license=('GPL')
url='http://www.manjaro.org'
groups=('base')
install='filesystem.install'
makedepends=('asciidoc')
depends=('iana-etc' 'bash' 'coreutils' 'nss-myhostname' 'manjaro-system')
backup=('etc/fstab' 'etc/crypttab' 'etc/group' 'etc/hosts' 'etc/ld.so.conf' 'etc/passwd'
        'etc/shadow' 'etc/gshadow' 'etc/resolv.conf' 'etc/motd' 'etc/nsswitch.conf'
        'etc/shells' 'etc/host.conf' 'etc/securetty' 'etc/profile' 'etc/issue' 'etc/rc.local'
        'etc/rc.local.shutdown' 'etc/hostname' 'etc/vconsole.conf' 'etc/locale.conf' 'etc/modules-load.d/modules.conf')
source=('crypttab' 'fstab' 'group' 'gshadow' 'host.conf' 'hostname' 'hosts' 'issue' 'ld.so.conf' 'locale.conf'
        'locale.sh' 'manjaro.7.txt' 'modprobe.d.usb-load-ehci-first' 'modules.conf' 'motd' 'nsswitch.conf' 'os-release'
        'passwd' 'profile' 'rc.local' 'rc.local.shutdown' 'resolv.conf' 'securetty' 'shadow' 'shells' 'vconsole.conf')
sha256sums=(ea8c67a34a0a8a41beabe3ceb5733c9e8b84b27c569c11ddbfd8395fce99d8dd
            86dff00854a7f2280bb799484ccc39ad449a171448926880742ae64f41666a43
            ba0a11a3b21e66137fd486d667ef01c59a70cd9f52942ef89dd8ea25d1a5e6b2
            9910787faf457cd978d06b53700941e603651987ec222cf3c357da570b540d7b
            f21f35e7d3083aeee19beb48478130d119b2572922a4889df98326d551540eda
            295a881e674d14ee0c0e77f25236a8fd99a9453e5c33f635f55d2e8c6959c7ea
            13d1f59c1d0860bf43da996e2b9a6ab2744791614241ed9a1376b7e7e30de3a8
            aa59e888f2f4b6f565ae7f4057b987bfde07890a2ccde438abee2b93a93d96c0
            00d5d103837480d26cd350753382c3211df6bc304ee59ffaab130fad108f6a86
            bc538cf42f96e24b8ec94c32242b6891de245e0207fab6937bfce3e23942742b
            1d30a33a066e5788110d8d8ad95444d08b95f10d979f895a63b0a12bbfa7e243
            0304f9811dfb2369212d1e30f65dd0be30fb0ccc7ad6f63c535cb5649d9f23de
            90fb123aa8c0176bc144322b585bb889181c437542ddbe546af43e11b95be6ab
            a8a1cd5c81b11498d43ba0e0b5de53de6f154a395d54171f44d2874b4f659053
            e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855
            3cfbffdf732985d8a5cd5145cca2da338df53bf45cd097f30c194294492f16f1
            c77720c6077823a98fd978cdde69cbdea1c550c03383dd31a4d516c02b7d977c
            84c8b6103553a88b43e83360446ea3da31e815b457eff233adfd636a304d399d
            d8d4d3eb8e2bc1f64de5b0d33cd6864558c0fe149cedb479fad36e0523060e60
            156b75b3470c2f73efca084b7ee681eca01508df3d8e9ba0c9325162b5d80f11
            4367e4f2b40237448cad791410a1acaedcd46f5504827840aa85214fad1b546c
            74d2be3dd69600bcf0da751a1804a386bf4ad143b76d79c336acaa5c600e7991
            f7b38167196943d8fb9d2e75358c7262d5fe1082b257c4c8caffe3b6de429cb5
            bdf92c2a97996eb1b3be24090ca94100f16232290010a7395e44c5c92bd38027
            6cff886fe9e23096d5f67b7857a747ebaf10431be5a2afc40236de5ad13a654f
            cd4a55177020a436254bb4baf84e068b98b3b0f6644173a7c853d58d236e00f1)

build() {
	cd ${srcdir}
	a2x -d manpage -f manpage manjaro.7.txt
}

package() {
	cd ${pkgdir}

	#
	# setup root filesystem
	#
	for d in boot dev etc home mnt usr var opt srv/http run; do
		install -d -m755 ${d}
	done
	install -d -m555 proc
	install -d -m555 sys
	install -d -m0750 root
	install -d -m1777 tmp
	# vsftpd won't run with write perms on /srv/ftp
	install -d -m555 -g ftp srv/ftp

	# setup /etc
	install -d etc/{ld.so.conf.d,skel,profile.d}
	for f in fstab group host.conf hosts issue ld.so.conf motd nsswitch.conf os-release passwd resolv.conf securetty shells profile hostname locale.conf vconsole.conf; do
		install -m644 ${srcdir}/${f} etc/
	done
	ln -s /proc/self/mounts etc/mtab
	for f in gshadow shadow crypttab; do
		install -m600 ${srcdir}/${f} etc/
	done


	# add modules-load.d/modules
	install -D -m644 ${srcdir}/modules.conf etc/modules-load.d/modules.conf

	# add rc.local and rc.local.shutdown
	install -m755 ${srcdir}/rc.local etc/
	install -m755 ${srcdir}/rc.local.shutdown etc/

	# Create the manjaro-release file
	echo "Manjaro Linux" > ${pkgdir}/etc/manjaro-release


	install -D -m644 ${srcdir}/modprobe.d.usb-load-ehci-first usr/lib/modprobe.d/usb-load-ehci-first.conf
	install -m755 ${srcdir}/locale.sh etc/profile.d/locale.sh

	# setup /var
	for d in cache/man local opt log/old lib/misc empty; do
		install -d -m755 var/${d}
	done
	install -d -m1777 var/{tmp,spool/mail}
	# allow setgid games to write scores
	install -d -m775 -g games var/games
	ln -s spool/mail var/mail
	ln -s ../run var/run
	ln -s ../run/lock var/lock

	#
	# setup /usr hierarchy
	#
	for d in bin include lib sbin share/misc src; do
		install -d -m755 usr/${d}
	done
	for d in $(seq 8); do
		install -d -m755 usr/share/man/man${d}
	done

	#
	# install manjaro(7) manpage
	#
	install -D -m644 ${srcdir}/manjaro.7 usr/share/man/man7/manjaro.7


	#
	# setup /usr/local hierarchy
	#
	for d in bin etc games include lib man sbin share src; do
		install -d -m755 usr/local/${d}
	done
	ln -s ../man usr/local/share/man
}
