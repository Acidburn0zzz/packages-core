# Based on the file created for Arch Linux by:
# Tom Gundersen <teg@jklm.no>

# Maintainer: Guinux <nuxgui@gmail.com>
# Maintainer: Roland <roland@manjaro.org>

pkgname=filesystem
pkgver=2012.8
pkgrel=2
pkgdesc='Base filesystem'
arch=('any')
license=('GPL')
url='http://www.manjaro.org'
groups=('base')
install='filesystem.install'
makedepends=('asciidoc')
depends=('iana-etc' 'bash' 'coreutils' 'manjaro-system')
backup=('etc/fstab' 'etc/crypttab' 'etc/group' 'etc/hosts' 'etc/ld.so.conf' 'etc/passwd'
        'etc/shadow' 'etc/gshadow' 'etc/resolv.conf' 'etc/motd' 'etc/nsswitch.conf'
        'etc/shells' 'etc/host.conf' 'etc/securetty' 'etc/profile' 'etc/issue' 'etc/rc.local'
        'etc/rc.local.shutdown' 'etc/hostname' 'etc/vconsole.conf' 'etc/locale.conf' 'etc/modules-load.d/modules.conf')
source=('group' 'issue' 'nsswitch.conf' 'securetty' 'host.conf' 'ld.so.conf'
        'passwd' 'shadow' 'fstab' 'crypttab' 'hosts' 'motd' 'os-release' 'resolv.conf'
        'shells' 'gshadow' 'profile' 'modprobe.d.usb-load-ehci-first' 'locale.sh' 'rc.local' 'rc.local.shutdown'
        'hostname' 'locale.conf' 'vconsole.conf' 'modules.conf')

build() {
	cd ${srcdir}
}

package() {
	cd ${pkgdir}

	#
	# setup root filesystem
	#
	for d in boot dev etc home media mnt usr var opt srv/http run; do
		install -d -m755 ${d}
	done
	install -d -m555 proc
	install -d -m555 sys
	install -d -m0750 root
	install -d -m1777 tmp
	# vsftpd won't run with write perms on /srv/ftp
	install -d -m555 -g ftp srv/ftp

	# setup /etc
	install -d etc/{ld.so.conf.d,skel,profile.d}
	for f in fstab group host.conf hosts issue ld.so.conf motd nsswitch.conf os-release passwd resolv.conf securetty shells profile hostname locale.conf vconsole.conf; do
		install -m644 ${srcdir}/${f} etc/
	done
	ln -s /proc/self/mounts etc/mtab
	for f in gshadow shadow crypttab; do
		install -m600 ${srcdir}/${f} etc/
	done


	# add modules-load.d/modules
	install -D -m644 ${srcdir}/modules.conf etc/modules-load.d/modules.conf

	# add rc.local and rc.local.shutdown
	install -m755 ${srcdir}/rc.local etc/
	install -m755 ${srcdir}/rc.local.shutdown etc/

	# Create the manjaro-release file
	echo "Manjaro Linux" > ${pkgdir}/etc/manjaro-release


	install -D -m644 ${srcdir}/modprobe.d.usb-load-ehci-first usr/lib/modprobe.d/usb-load-ehci-first.conf
	install -m755 ${srcdir}/locale.sh etc/profile.d/locale.sh

	# setup /var
	for d in cache/man local opt log/old lib/misc empty; do
		install -d -m755 var/${d}
	done
	install -d -m1777 var/{tmp,spool/mail}
	# allow setgid games to write scores
	install -d -m775 -g games var/games
	ln -s spool/mail var/mail
	ln -s ../run var/run
	ln -s ../run/lock var/lock

	#
	# setup /usr hierarchy
	#
	for d in bin include lib sbin share/misc src; do
		install -d -m755 usr/${d}
	done
	for d in $(seq 8); do
		install -d -m755 usr/share/man/man${d}
	done


	#
	# setup /usr/local hierarchy
	#
	for d in bin etc games include lib man sbin share src; do
		install -d -m755 usr/local/${d}
	done
	ln -s ../man usr/local/share/man
}



md5sums=('45940618da782cc391c59f88ab3333c3'
         'd87f36842a580ff7bb91a4dee2c66507'
         '13753e4e0964f3652b0cc60a28528bdf'
         '4c4540eeb748bf1f71d631b8c1dcf0b3'
         'f28150d4c0b22a017be51b9f7f9977ed'
         '6e488ffecc8ba142c0cf7e2d7aeb832e'
         '8a9042a2cedf6b6b47eb8973f14289cb'
         'b8355d9d2782f424f4cedcf682651be0'
         'ca716f853860199c1286e7939b2f2666'
         '1745349eb24ed21b4cfaa6f423bddb76'
         '7bc65f234dfb6abf24e7c3b03e86f4ff'
         'd41d8cd98f00b204e9800998ecf8427e'
         '6b52a629b718789cdc40646f9d9842d5'
         '6f48288b6fcaf0065fcb7b0e525413e0'
         '22518e922891f9359f971f4f5b4e793c'
         'f95416882cef800edef08382a1176b7d'
         'f3b6ae7db8adffaaa4bffc6099dcbd50'
         'a8a962370cd0128465d514e6a1f74130'
         '3807d07215d9116331fe1cf8feeaa0f8'
         'bd236a57bc00aa4727b98c5ae401c457'
         '669ce6d766dd9deb86686219f3763fe4'
         'f985ed56bce013283faa97152d6e8565'
         '53d73274b6bdbe4837e313bca18539fe'
         '37cebc2d300c3099e91d388adbf11231'
         '5219472673582f31fd068ae328ec1145')
