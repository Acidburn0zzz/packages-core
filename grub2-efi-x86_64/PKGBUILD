# Based on the file created for Arch Linux by:
# Ronald van Haren <ronald.archlinux.org>
# Keshav P R <(the.ridikulus.rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>

# Maintainer: Guinux <nuxgui@gmail.com>

_grub_bzr_rev="4232"
_pkgver="2.00~beta4"

_grub_lua_ver=24
_grub_gpxe_ver=12

pkgname=grub2-efi-x86_64
pkgver=2.00beta4
pkgrel=4
epoch=1
pkgdesc="The GNU GRand Unified Bootloader version 2 - x86_64 UEFI version"
url="http://www.gnu.org/software/grub/"
arch=('any')
license=('GPL3')

makedepends=('xz' 'python' 'autogen' 'texinfo' 'help2man' 'gettext' 'device-mapper' 'fuse')
depends=("grub2-common=${epoch}:${pkgver}" 'dosfstools' 'efibootmgr')

options=('!strip' '!emptydirs')

# source=("ftp://ftp.archlinux.org/other/grub2/grub_r${_grub_bzr_rev}.tar.xz"
source=("http://alpha.gnu.org/gnu/grub/grub-${_pkgver}".tar.xz
        "ftp://ftp.archlinux.org/other/grub2/grub2_extras_lua_r${_grub_lua_ver}.tar.xz"
        "ftp://ftp.archlinux.org/other/grub2/grub2_extras_gpxe_r${_grub_gpxe_ver}.tar.xz")

noextract=("grub2_extras_lua_r${_grub_lua_ver}.tar.xz"
           "grub2_extras_gpxe_r${_grub_gpxe_ver}.tar.xz")

sha256sums=('628a17fefafcbd90cdab25220ef3ae717558dde73e55adf83dfee3aae55e390e'
            'bfe0318a9bb7edbaee2489b1396d0034791751b42035ea40ad61600fdf974df2'
            '8ce1b6b89ef0e3545736477d4c4418524e32c7fb9ba40c67dcebfb84b98cde66')

build() {
	if [[ "${CARCH}" == 'i686' ]]; then
		echo "This package can be built only in a x86_64 system. Exiting."
		exit 1
	fi

	cd "${srcdir}/grub-${_pkgver}"

	## add grub-extras
	export GRUB_CONTRIB="${srcdir}/grub-${_pkgver}/grub-extras/"
	install -d "${srcdir}/grub-${_pkgver}/grub-extras"

	bsdtar xf "${srcdir}/grub2_extras_lua_r${_grub_lua_ver}.tar.xz" \
		-C "${srcdir}/grub-${_pkgver}/grub-extras"

	bsdtar xf "${srcdir}/grub2_extras_gpxe_r${_grub_gpxe_ver}.tar.xz" \
		-C "${srcdir}/grub-${_pkgver}/grub-extras"

	## start the actual build process
	cd "${srcdir}/grub-${_pkgver}/"
	./autogen.sh

	CFLAGS="" ./configure \
		--with-platform="efi" \
		--target="x86_64" \
		--host="${CARCH}-unknown-linux-gnu" \
		--disable-efiemu \
		--enable-mm-debug \
		--enable-nls \
		--enable-device-mapper \
		--enable-cache-stats \
		--enable-grub-mkfont \
		--enable-grub-mount \
		--prefix="/usr" \
		--bindir="/usr/bin" \
		--sbindir="/usr/sbin" \
		--mandir="/usr/share/man" \
		--localedir="/usr/share/locale" \
		--infodir="/usr/share/info" \
		--datadir="/usr/lib" \
		--datarootdir="/usr/share" \
		--sysconfdir="/etc" \
		--program-prefix="" \
		--with-bootdir="/boot" \
		--with-grubdir="grub" \
		--disable-werror

	CFLAGS="" make
}

package() {
	cd "${srcdir}/grub-${_pkgver}"
	make DESTDIR="${pkgdir}/" install

	## remove non platform-specific files
	rm -rf "${pkgdir}"/{boot,etc,usr/{share,bin,sbin}}

	## remove gdb debugging related files
	rm -f "${pkgdir}/usr/lib/grub/x86_64-efi"/*.module || true
	rm -f "${pkgdir}/usr/lib/grub/x86_64-efi"/*.image || true
	rm -f "${pkgdir}/usr/lib/grub/x86_64-efi"/{kernel.exec,gdb_grub,gmodule.pl} || true

	## remove conflicting common files
	rm -fr "${pkgdir}/usr/lib/grub/themes"
	rm -f "${pkgdir}/usr/lib/grub/"*.h
	rm -f "${pkgdir}/usr/lib/grub/"*.pf2
	rm -f "${pkgdir}/usr/lib/grub/grub-mkconfig_lib"
}
