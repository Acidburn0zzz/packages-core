# Based on the file created for Arch Linux by:
# Sergej Pupykin <pupykin.s+arch@gmail.com>
# Krzysztof Raczkowski <raczkow@gmail.com>

# Maintainer: Guinux <nuxgui@gmail.com>
# Co-Maintainer: Clittle <philm@manjarolinux.org>

pkgname=linux34-open-vm-tools-modules
_pkgname=open-vm-tools-modules
groups=('linux34-extramodules')
pkgver=2012.05.21
_pkgsubver=724730
pkgrel=2
pkgdesc="kernel modules for the open source implementation of VMware Tools"
arch=('i686' 'x86_64')
url="http://open-vm-tools.sourceforge.net/"
license=('GPL')
makedepends=('libdnet' 'icu' 'uriparser' 'linux34-headers')
depends=("linux34")
install=$_pkgname.install
options=('!strip')
source=("http://downloads.sourceforge.net/open-vm-tools/open-vm-tools-$pkgver-${_pkgsubver}.tar.gz"
	"modprobe.conf")
md5sums=('91affb5b23db8abceff930613845f434'
         'ac9b8e3cb798f5056ca92767ec705117')

build() {
  _kernver=`pacman -Q linux34 | cut -d . -f 2 | cut -f 1 -d -`
  KERNEL_RELEASE=`cat /usr/lib/modules/extramodules-3.${_kernver}-MANJARO/version`

  cd "$srcdir/open-vm-tools-${pkgver}-${_pkgsubver}"
  sed -i 's|proc-3.2.8|procps|g' configure
  [ $NOEXTRACT -eq 1 ] || ./configure \
      --prefix=/usr \
      --without-x \
      --with-linuxdir=/usr/lib/modules/$KERNEL_RELEASE

  make -C modules modules
}

package() {
  _kernver=`pacman -Q linux34 | cut -d . -f 2 | cut -f 1 -d -`
  depends=("linux>=3.${_kernver}" "linux<3.`expr ${_kernver} + 1`")
  KERNEL_VERSION=`cat /usr/lib/modules/extramodules-3.${_kernver}-MANJARO/version`
  msg "Kernel = $KERNEL_VERSION"

  cd "$srcdir/open-vm-tools-${pkgver}-${_pkgsubver}"
  mkdir -p $pkgdir/usr/lib/modules/extramodules-3.${_kernver}-MANJARO/
  for MOD in `find -type f -name '*.ko'`; do
    install -Dm644 $MOD $pkgdir/usr/lib/modules/extramodules-3.${_kernver}-MANJARO/
  done
  gzip $pkgdir/usr/lib/modules/extramodules-3.${_kernver}-MANJARO/*.ko

  install -D -m 644 ${srcdir}/modprobe.conf ${pkgdir}/etc/modprobe.d/${pkgname}.conf
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extramodules'/" "${startdir}/${_pkgname}.install"
}
